plugins {
	id 'org.springframework.boot' version '2.1.7.RELEASE'
	id 'java'
	id 'groovy'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.usaywhat'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	integTestImplementation.extendsFrom implementation
	integTestRuntimeOnly.extendsFrom runtimeOnly
}

sourceSets {
	integTest {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

repositories {
	mavenCentral()
}

dependencies {
	annotationProcessor 'org.projectlombok:lombok'
	compileOnly 'org.projectlombok:lombok'
	
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'com.fasterxml.jackson.module:jackson-module-parameter-names'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	
	runtimeOnly 'org.postgresql:postgresql'


	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	integTestImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
	integTestImplementation 'org.spockframework:spock-spring:1.3-groovy-2.5'
	integTestImplementation 'org.testcontainers:postgresql:1.11.4'
	integTestImplementation 'org.testcontainers:spock:1.11.3'
	integTestImplementation 'org.flywaydb:flyway-core'
	integTestImplementation 'org.springframework.boot:spring-boot-starter-test'
	integTestImplementation 'com.fasterxml.jackson.core:jackson-databind'
	integTestImplementation 'com.athaydes:spock-reports:1.6.2'
}

task integTest(type: Test) {
	description = 'Run integration tests.'
	group = 'verification'

	testClassesDirs = sourceSets.integTest.output.classesDirs
	classpath = sourceSets.integTest.runtimeClasspath
	shouldRunAfter test
}

check.dependsOn integTest
